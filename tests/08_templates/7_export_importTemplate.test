--- Q7 testcase ---
Format-Version: 1.0
Q7-vendor: www.xored.com/q7
Contexts: _u4EL4MmsEeGLNoFQBj_CGw,_mSYdkMmuEeGLNoFQBj_CGw,_STqwQNTOEeGKjezHpf7UXA,_y8oG0NTLEeGKjezHpf7UXA
Element-Name: 7_export_importTemplate
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _GOpmUNVqEeGFMoSoUTDWjQ
Save-Time: 9/24/12 4:40 PM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

*** Context ***
+ Clear Workspace
+ A Project is created in the workspace without Execution Environment
+ A template is created
 
*** Actions ***
- Window -> Preferences
- Lua -> Editor -> Templates
- Select the template to export -> Export...
- Select the exported template -> Remove
- Import... -> Select the previously exported template
 
*** Assertions ***
# Check the imported template is in the templates list
# Check all the fields of the template correspond to the previously created/exported template
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

/*********************
*	Created by: Simon Gaestel
*	Creation date: 07/24/2012
*********************/

// Export an existing template, then remove it
get-menu "Window/Preferences" | click
get-window Preferences | get-table | select newTemplate
set-dialog-result File "platform:/resource/../q7-external-files/templates.xml"
with [get-window Preferences] {
    get-button "Export..." | click
    get-window "Export Templates" | get-button Yes | click
    get-table | select newTemplate
    get-button Remove | click
}

// Import the previously exported/removed template
set-dialog-result File "platform:/resource/../q7-external-files/templates.xml"
with [get-window Preferences] {
    get-button "Import..." | click
    get-table | select newTemplate

// Check all data from the template have been imported
    get-button "Edit..." | click
    with [get-window "Edit Template"] {
	    get-editbox -after [get-label "Name:"] | get-property text | equals newTemplate | verify-true
	    get-editbox -after [get-label "Description:"] | get-property text | equals newTemplateDesc | verify-true
	    get-editbox -after [get-label "Pattern:"] | get-property text 
	        | equals "function new (${var1}, ${var2}, ${var3}, ${language})\n"
	        + "${var1} = ${module_name}\n"
	        + "${var2} = ${module_short_name}\n"
	        + "${var3} = ${file}\n"
	        + "end\n"
	        + "${cursor}" | verify-true
		get-button OK | click
    }

// Remove the template
    get-table | select newTemplate
    get-button Remove | click
    get-button OK | click
}

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
